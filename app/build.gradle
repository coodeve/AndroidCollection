
apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
// 自定义插件
apply plugin: 'lifecycle-plugin'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.coodev.androidcollection"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }

        javaCompileOptions {
            annotationProcessorOptions {
                // 设置Room升级版本的历史记录位置
                arguments = ["room.schemaLocation": "$projectDir/schemes".toString(),
                             AROUTER_MODULE_NAME: project.getName()]
            }
        }

    }

    dataBinding {
        enabled = true
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.10.2"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

}

// jar包版本配置
def version_name = "1.0.0"
def version_code = 1;
def include_package = "top.coodev"
def jar_file_name_debug = "dev_sdk$version_name-$version_code-debug.jar"
def jar_file_name_release = "dev_sdk$version_name-$version_code-release.jar"
def out_dir_name = "lib"

// 创建jar包
task buildJar(type: Jar, dependsOn: ['build']) {
    archiveName "$jar_file_name_debug"
    destinationDir file("$out_dir_name")
    from('build/intermediates/javac/debug/compileDebugJavaWithJavac/classes')
    from fileTree(dir: 'src/main', includes: ['assets/**'])
    include "$include_package"
    include "assest/**"
    exclude "**/BuildConfig.class"
    exclude "**/BuildConfig\$.class"
    exclude "**/R.class"
    exclude "**/R\$.class"
    println "jar file $jar_file_name_debug compiled "
}
// jar混淆
task buildJarProguard(type: proguard.gradle.ProGuardTask, dependsOn: buildJar) {
    injars "$jar_file_name_debug"
    outjars "lib/$jar_file_name_release"
    configuration "proguard-rules.pro"
    println "jar file $jar_file_name_release compiled "
}

// 调用window cmd
task buildAndInstall(type:Exec,description:'build and install after signapk'){
    // 需要在doFirst中执行，否则Exec命令会出错
    doFirst {
        println "task[$name] is running..."
        copy {
            from "build/outputs/apk/debug/app-debug.apk"
            into "E:\\Assest\\Auto_sign_s\\sign_S"
            rename {
                String fileName ->
                    "123.apk"
            }
        }
        workingDir 'E:\\Assest\\Auto_sign_s\\sign_S'
        commandLine 'cmd',"/c","APK_Sign_Platform_123"
        println "task[$name] end."
    }

}
// 设置到build之后执行
build.finalizedBy(buildAndInstall)

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    // lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    // viewmodel
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.2.0"
    //room
    implementation 'androidx.room:room-runtime:2.2.5'
    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    // workmanager
    implementation 'androidx.work:work-runtime:2.4.0'
    // page
    implementation 'androidx.paging:paging-runtime:2.1.2'
    // material
    implementation 'com.google.android.material:material:1.2.1'
    // recyclerview
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    // butterknife
    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'
    // rxjava ,rxandroid
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    // eventbus
    implementation 'org.greenrobot:eventbus:3.1.1'
    // glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'jp.wasabeef:glide-transformations:2.0.1'
    // BaseRecyclerViewAdapterHelper
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.40'
    // basepopu
    implementation 'com.github.razerdp:BasePopup:2.1.4'
    // 权限申请
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    // For developers using the Android Support Library
    implementation 'pub.devrel:easypermissions:2.0.1'
    // photoview
    implementation 'com.bm.photoview:library:1.4.1'
    // photoView
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    // utilcode
    implementation 'com.blankj:utilcode:1.25.9'
    //抖音分享
    implementation 'com.bytedance.ies.ugc.aweme:open-sdk:0.0.1.4'
    // apache common
    implementation 'org.apache.commons:commons-lang3:3.7'
    // gson
    implementation 'com.google.code.gson:gson:2.8.6'
    // okhttp & okio
    implementation 'com.squareup.okhttp3:okhttp:4.8.0'
    implementation 'com.squareup.okio:okio:2.7.0'
    // cronet
    implementation 'com.google.android.gms:play-services-cronet:17.0.0'
    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    // 标签控件
    implementation 'com.github.donkingliang:LabelsView:1.5.0'
    //xUtils3
    implementation 'org.xutils:xutils:3.5.0'
    // jetpack navigation
    def nav_version = "2.3.2"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    // greenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    // ARouter
    implementation "com.alibaba:arouter-api:1.5.1"
    annotationProcessor "com.alibaba:arouter-compiler:1.5.1"
    // apt
    implementation project(':app_apt_annotation')
//    annotationProcessor project(path: ':app_apt_compiler')
    // xlog
    implementation 'com.elvishew:xlog:1.6.1'

}
